buildscript {
  repositories { mavenCentral() }
  dependencies { classpath 'org.ajoberstar:gradle-git:0.5.0' }
}

import org.ajoberstar.gradle.git.tasks.*

apply from: "config.gradle"

task update
task build
task buildExamples
task gitstatus

def isCollection(obj) {
  return [Collection, Object[]].any { it.isAssignableFrom(obj.getClass()) }
}

def toCollection(obj) {
  return isCollection(obj) ? obj : [ obj ]
}

for(def proj in project.ext.projects) {

  def projName = proj instanceof String ? proj : proj.name
  if(proj instanceof String)
    proj = []
  File projFolder = file(projName)
  
  def projUpdateTask;
  if(projFolder.exists()) {
    projUpdateTask = task(projName + "_update", type: GitPull) {
      repoPath = projFolder.absolutePath
    }
    if(proj.branch)
      task(projName + "_checkout", type: GitCheckout) { task ->
        repoPath = projFolder.absolutePath
        branchName = proj.branch
        projUpdateTask.dependsOn task
      }
  } else {
    def gitSource;
    if(proj.gitSource)
      gitSource = proj.gitSource 
    else {
      def gitName = proj.gitName ? proj.gitName : projName + ".git"      
      def gitBase
      if(proj.gitBase)
        gitBase = proj.gitBase
      else if(ext.has("gitBase"))
        gitBase = ext.gitBase
      else
        throw new Exception("Don't know from where to clone project '$projName'. Please specify gitBase or gitSource.");
      gitSource = gitBase
      if(!gitSource.endsWith("/"))
        gitSource += "/"
      gitSource += gitName;
    }
    projUpdateTask = task(projName + "_update", type: GitClone) {
      destinationPath = projFolder.absolutePath
      uri = gitSource
      if(proj.branch) {
        checkout = true
        branch = proj.branch
      }
    }
  }  
  update.dependsOn projUpdateTask
  
  def projBuildTask = null;
  if(proj.build) {
    projBuildTask = task(projName + "_build", type: GradleBuild) { task ->
      dir = new File(projFolder, proj.build instanceof String ? proj.build : ".")
      tasks = proj.buildTasks ? proj.buildTasks : [ "build" ]
      build.dependsOn task
      if(projFolder.exists())
        task.mustRunAfter projUpdateTask
      else
        task.dependsOn projUpdateTask
    }
  } else if(!projFolder.exists())
    build.dependsOn projUpdateTask
    
  if(proj.examples)
    task(projName + "_buildExamples", type: GradleBuild) { task ->
      tasks = [ "build" ]
      dir = new File(projFolder, proj.examples instanceof String ? proj.examples : "examples")
      dependsOn build
      buildExamples.dependsOn task
      if(projBuildTask)
        dependsOn projBuildTask
      else if(projFolder.exists())
        mustRunAfter projUpdateTask
      else
        dependsOn projUpdateTask
    }

  task(projName + "_gitstatus", type: GitStatus) { task ->
    description = "git status on " + projName
    repoPath = projFolder.absolutePath
    doLast {
      [ "added", "changed", "conflicting", "missing", "modified", "removed", "untracked" ].each { fileSetName ->
        task[fileSetName].files.each { file ->
          println "${fileSetName}: ${file.absolutePath}"
        }
      }
      task.untrackedDirs.files.each { file ->
        if(!task.ignored.files.contains(file))
          println "untracked dir: " + file.absolutePath
      }
    }
    if(projFolder.exists())
      mustRunAfter projUpdateTask
    else
      dependsOn projUpdateTask
    mustRunAfter update
    gitstatus.dependsOn task
  }
}

// second pass, now we handle inter-project dependencies
for(def proj in project.ext.projects) {
  def projName = proj instanceof String ? proj : proj.name
  if(proj instanceof String)
    proj = []
  if(proj.dependsOn) {
    def thisProjUpdate = tasks[projName + "_update"]
    def thisProjBuild = tasks.findByName(projName + "_build")
    def thisProjGitStatus = tasks[projName + "_gitstatus"]
    toCollection(proj.dependsOn).each { otherProj ->
      def otherProjUpdate = otherProj + "_update"
      thisProjUpdate.dependsOn otherProjUpdate
      if(thisProjBuild) {
        def otherProjBuild = tasks.findByName(otherProj + "_build")
        if(otherProjBuild)
          thisProjBuild.dependsOn otherProjBuild
        else if (!file(otherProj).exists())
          thisProjBuild.dependsOn otherProjUpdate
      }
      thisProjGitStatus.dependsOn otherProj + "_gitstatus"
    }
  }
}

defaultTasks "build"
